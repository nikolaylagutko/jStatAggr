buildscript {
	repositories {
		mavenCentral() 
		jcenter()
	}
	
	dependencies {
		classpath group: 'nl.javadude.gradle.plugins',	name: 'license-gradle-plugin',	version: gradleLicenseVersion
		classpath group: 'com.github.townsfolk',		name: 'gradle-release',			version: gradleReleaseVersion
		classpath group: 'com.jfrog.bintray.gradle',	name: 'gradle-bintray-plugin',	version: gradleBintrayVersion
		classpath group: 'org.gerzog.gradle',			name: 'gerzog-gradle-ide',		version: gerzogPluginsVersion
	}
}

apply plugin: 'release'

subprojects {
	apply plugin: 'java'
	apply plugin: 'groovy'
	apply plugin: 'gerzog-eclipse'
	apply plugin: 'license'
	
	sourceCompatibility = 1.7
	targetCompatibility = 1.7
	
	repositories {
		mavenCentral()
	}	
	
	dependencies {
		testCompile group: 'junit',					name: 'junit-dep',		version: junitVersion
		testCompile group: 'org.codehaus.groovy',	name: 'groovy-all',		version: groovyVersion
		testCompile group: 'org.spockframework',	name: 'spock-core',		version: spockVersion
		testCompile group: 'cglib',					name: 'cglib-nodep',	version: cglibVersion
		testCompile group: 'org.objenesis',			name: 'objenesis',		version: objenesisVersion		
	}
	
	license {
		header rootProject.file("HEADER")
	}
	
	task javadocJar(type: Jar, dependsOn: javadoc) {
		classifier = 'javadoc'
		from 'build/docs/javadoc'
	}
	
	task sourcesJar(type: Jar) {
		from sourceSets.main.allSource
		classifier = 'sources'
	}
}

release {
	requireBranch = 'java7'
}

task wrapper(type: Wrapper) {
	gradleVersion = gradleWrapperVersion
}
