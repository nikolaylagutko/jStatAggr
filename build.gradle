buildscript {
	repositories {
		mavenCentral() 
		jcenter()
	}
	
	dependencies {
		classpath group: 'nl.javadude.gradle.plugins',	name: 'license-gradle-plugin',	version: gradleLicenseVersion
		classpath group: 'au.com.ish.gradle',			name: 'release',				version: gradleReleaseVersion, {
			exclude module: 'svnkit'
		}
		classpath group: 'com.jfrog.bintray.gradle',	name: 'gradle-bintray-plugin',	version: gradleBintrayVersion
	}
}

apply plugin: 'release'

subprojects {
	apply plugin: 'java'
	apply plugin: 'groovy'
	apply plugin: 'eclipse'
	apply plugin: 'license'
	
	sourceCompatibility = 1.7
	targetCompatibility = 1.7
	
	repositories {
		mavenCentral()
	}	
	
	dependencies {
		testCompile group: 'junit',					name: 'junit-dep',		version: junitVersion
		testCompile group: 'org.codehaus.groovy',	name: 'groovy-all',		version: groovyVersion
		testCompile group: 'org.spockframework',	name: 'spock-core',		version: spockVersion
		testCompile group: 'cglib',					name: 'cglib-nodep',	version: cglibVersion
		testCompile group: 'org.objenesis',			name: 'objenesis',		version: objenesisVersion		
	}
	
	license {
		header rootProject.file("HEADER")
	}
	
	task javadocJar(type: Jar, dependsOn: javadoc) {
		classifier = 'javadoc'
		from 'build/docs/javadoc'
	}
	
	task sourcesJar(type: Jar) {
		from sourceSets.main.allSource
		classifier = 'sources'
	}
}

release {
	scm = 'git'
	allowLocalModifications = true
}

task wrapper(type: Wrapper) {
	gradleVersion = gradleWrapperVersion
}
