apply plugin: 'bintray'
apply plugin: 'maven-publish'

def pomConfig = {
	resolveStrategy = Closure.DELEGATE_FIRST
	description 'Java library to simplify Statistics Aggregation'
	licenses {
		license {
			name 'The Apache Software License, Version 2.0'
			url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
			distribution 'repo'
		}
	}
	developers {
		developer {
			id 'nikolaylagutko'
			name 'Nikolay Lagutko'
			email 'NikolayLagutko@mail.com'
		}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId project.name.toLowerCase()
			
			from components.java
			artifact sourcesJar
			artifact javadocJar
			
			pom.withXml {
				asNode().children().last() + pomConfig
			}
		}
	}
}

bintray {
	user = 'nikolaylagutko'
	key = System.env.BINTRAY_API_KEY
	
	publications = ['mavenJava']
	
	pkg {
		repo = 'gerzog-java-libraries'
		name = 'jStatAggr'
		desc = 'Statistics Aggregator'
		licenses = ['Apache-2.0']
	}
	
	dryRun = version.endsWith("SNAPSHOT")
}

task install(dependsOn: 'publishMavenJavaPublicationToMavenLocal') << {
	logger.info "Installing $project.name"
}

bintrayUpload.dependsOn install
